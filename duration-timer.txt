' Script name: DuratronTimer
' This script calculates the remaining time of the current input and
' updates this time in the Text input named "Text Middle Centre Outline.gtzip".
' If the current input has no duration, it will start counting up.

Console.WriteLine("DurationTimer 1.0.1")

Dim textInputTitle = "Text Middle Centre Outline.gtzip"
Dim position as double = 0
Dim duration as double = 0
Dim yellowTime as integer = 60
Dim redTime as integer = 11

Do While True
    Sleep(300)

    Dim xml as string = API.XML()
    Dim x as new system.xml.xmldocument
    x.loadxml(xml)

    Dim activeInput as string = (x.SelectSingleNode("//active").InnerText)
    Dim durationNode As XmlNode = x.SelectSingleNode("//input[@number='" & activeInput & "']/@duration")
    If durationNode IsNot Nothing Then
        duration = Double.Parse(durationNode.Value)
    Else
        Console.WriteLine("Node not found for activeInput: " & activeInput)
        duration = 0
    End If

    Dim positionNode As XmlNode = x.SelectSingleNode("//input[@number='"& activeInput &"']/@position")
    If positionNode IsNot Nothing Then
        position = Double.Parse(positionNode.Value)
    Else
        console.writeline("Node not found for activeInput: " & activeInput)
        position = 0
    End If


    If duration = 0
        API.Function("SetTextColour", textInputTitle, "white")
        API.Function("StartCountdown", textInputTitle)
        continue do
    Else
        API.Function("StopCountdown", textInputTitle)
    End if

    Dim timeLeft as integer = CInt((duration - position) / 100)
    timeLeft = timeLeft / 10

    Dim Minutes as integer = timeLeft \ 60
    Dim Seconds as integer = timeLeft Mod 60

    Dim timeStr as string = Seconds.ToString()
    If Minutes > 0
        timeStr = Minutes.ToString("00") + ":" + Seconds.ToString("00")
    End If
    API.Function("SetText", textInputTitle, timeStr)

    If timeLeft < redTime
        API.Function("SetTextColour", textInputTitle, "red")
    Else If timeLeft < yellowTime
        API.Function("SetTextColour", textInputTitle, "orange")
    Else
        API.Function("SetTextColour", textInputTitle, "green")
    End If
Loop
